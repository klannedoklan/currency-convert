plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.openapi.generator' version '7.9.0'
}

ext {
	springCloudVersion = "2023.0.3"
	springDocVersion = "2.6.0"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	implementation 'com.h2database:h2'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.10'
	implementation 'io.swagger.core.v3:swagger-models:2.2.10'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
	implementation 'jakarta.validation:jakarta.validation-api:3.0.2'

	// Springdoc
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-api:$springDocVersion"
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$springDocVersion"

	compileOnly 'org.projectlombok:lombok'

	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "org.junit.platform:junit-platform-suite"

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

openApiGenerate {
	generatorName = 'spring'
	inputSpec = "$rootDir/src/main/resources/public/currency-convert-api.yaml"
	outputDir = "$rootDir/build/generated"
	apiPackage = 'com.example.currencyconvert.api'
	modelPackage = 'com.example.currencyconvert.model'
	groupId = 'com.example.currencyconvert'
	configOptions = [
			useJakartaEe: 'true',
			apiTests: 'false',
			modelTests: 'false',
			interfaceOnly: 'true'
	]
}

java {
	sourceSets {
		main {
			java.srcDirs += "src/generated/java"
			resources.srcDirs += "src/generated/resources"
		}
	}
	toolchain {
		languageVersion = JavaLanguageVersion.of(application_java_version)
	}
}

bootJar {
	archiveFileName = 'currency-convert.jar'
}

compileJava {
	dependsOn tasks.openApiGenerate
	source tasks.openApiGenerate.outputs
}

processResources {
	from 'publicFolder'
}

tasks.register('publicFolder', Copy) {
	from 'public' into 'public'
}